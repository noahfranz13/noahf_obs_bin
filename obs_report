#! /bin/csh

if (x == x$1) then
  echo 'obs_report base_dir (where base_dir is a name like "AGBT16A_999_233")'
  exit 0
endif

if (`hostname` != "bls0") then
  echo 'have to be on bls0 to run.'
  exit 0
endif

set BASEDIR = $1

set ALLOBJ = /tmp/obs_report.objs.$$
set UNIQOBJ = /tmp/obs_report.uobjs.$$
set ALLDIRS = /tmp/obs_report.alldirs.$$

(find /mnt_*/datax* -name $BASEDIR -print > $ALLDIRS) >& /dev/null

echo "Observation $BASEDIR located in the following nodes:"
echo ""
cat $ALLDIRS
set TOTALNODES = `wc -l $ALLDIRS | awk '{print $1}'`
echo "Total nodes: $TOTALNODES"
echo ""

echo "Objects:"
echo ""
cat /dev/null > $ALLOBJ
foreach DIR (`cat $ALLDIRS`)
  cd $DIR/*/*
  # take off blcN_ prefix, and all currently know suffixes to get object names:
  ls | sed 's/blc[0-9]_//' | sed 's/blc[0-9][0-9]_//' | sed 's/.[0-9][0-9][0-9][0-9].raw$//' | awk -F'.gpuspec.' '{print $1}' | sed 's/.raw_files_deleted$//' >> $ALLOBJ
end  

sort $ALLOBJ | uniq > $UNIQOBJ

set TOTALOBS = `wc -l $UNIQOBJ | awk '{print $1}'`

foreach OBJ (`cat $UNIQOBJ`)

  set TARGET = `echo $OBJ | awk -Fguppi_ '{print $2}' | sed 's/[0-9][0-9][0-9][0-9][0-9]_[0-9][0-9][0-9][0-9][0-9]_//' | sed 's/_[0-9][0-9][0-9][0-9]$//'`
  set COUNT = `grep -c "_${TARGET}_" $UNIQOBJ`
  set PRINTOBJ = $OBJ
  if ($COUNT > 1) then
    set PRINTOBJ = "${OBJ}+"
  endif

  echo $PRINTOBJ | awk '{printf("%-50s : ",$1)}'
  
  echo -n '[raw] ' 
  foreach DIR (`cat $ALLDIRS`)
    ls $DIR/*/*/*${OBJ}*.raw |& grep -v "No match" | wc -l | awk '{printf("%2d",$1)}'
    ls -l $DIR/*/*/*${OBJ}*gpuspec..headers |& awk '{if ($5!="") printf("-%4d ",($5/6400)); else printf("-     ")}'
  end
  echo ""
  # echo "" | awk '{printf("%-50s   ",$1)}'
  echo $PRINTOBJ | awk '{printf("%-50s : ",$1)}'
    
  echo -n '[fil] ' 
  foreach DIR (`cat $ALLDIRS`)
    ls $DIR/*/*/*${OBJ}*.fil |& grep -v "No match" | wc -l | awk '{printf("%2d",$1)}'
    ls -l $DIR/*/*/*${OBJ}*.fil |& grep -v "No match" | awk '{n=n+$5}; END {printf("-%4d ",n/(1024*1024))}'
  end

#  echo -n '[total fil sizes] '
#  set FIRST = 1
#  foreach DIR (`cat $ALLDIRS`)
#    set SIZE = `ls -l $DIR/*/*/*${OBJ}*.fil |& grep -v "No match" | awk '{n=n+$5}; END {printf("%.0f",n)}'`
#    if ($FIRST == 1) then
#      set FIRST = 0
#      set MAINSIZE = $SIZE
#      echo -n "$MAINSIZE "
#    else
#      echo $SIZE $MAINSIZE | awk '{printf("%.0f ",$1-$2)}'
#    endif
#  end

  echo ""
  echo ""
  
end # foreach OBJ

echo "Total objects observed: $TOTALOBS"

echo ""
echo "Length of time (seconds) between first/last gpuspecs in these directories:"

foreach DIR (`cat $ALLDIRS`)
  cd $DIR/*/*
  # take off blcN_ prefix, and all currently know suffixes to get object names:
  set FIRST = `ls -l --time-style="+%s" *.gpuspec.done |& grep -v " No " | sort -n -k +5 | awk '{print $6}' | head -1`
  set LAST = `ls -l --time-style="+%s" *.gpuspec.done |& grep -v " No " | sort -n -k +5 | awk '{print $6}' | tail -1`
  set DELTA = 0
  if (x != x$FIRST) then
    set DELTA = `echo $LAST $FIRST | awk '{print $1-$2}'`
  endif
  echo $DIR : $DELTA
end  

# cleanup
/bin/rm $ALLOBJ
/bin/rm $UNIQOBJ
/bin/rm $ALLDIRS
